uniform vec3 uPlayerPosition;
uniform float uGrassSize;
uniform sampler2D uPerlinTexture;
uniform float uTime;
uniform vec2 uWindDirection;
uniform float uWindSpeed1;
uniform float uWindSpeed2;
uniform float uWindNoiseScale1;
uniform float uWindNoiseScale2;
uniform float uWindStrength;
uniform sampler2D uInteractivePlaneTexture;

attribute vec2 center;

#include ../includes/getRotatePivot2d.glsl

varying vec3 vColor;

void main()
{

    // center
    vec2 newCenter = center;

    // move the grass oppersite to the player position
    newCenter -= uPlayerPosition.xz;

    // lower the grass on z


    // infinite grass
    float halfSize = uGrassSize * 0.5;
    newCenter.x = mod(newCenter.x + halfSize, uGrassSize) - halfSize;
    newCenter.y = mod(newCenter.y + halfSize, uGrassSize) - halfSize;

    vec4 modelCenter = modelMatrix * vec4(newCenter.x, 0.0, newCenter.y, 1.0);

    vec4 modelPosition = modelMatrix * vec4(position, 1.0);

    // grass to center
    modelPosition.xz += newCenter;

    // tip of the blade
    float tip = step(2.0, mod(float(gl_VertexID) + 1.0, 3.0));

    // billboarding
    float angleToCamera = atan(modelCenter.x - cameraPosition.x, modelCenter.z - cameraPosition.z);
    modelPosition.xz = getRotatePivot2d(modelPosition.xz, angleToCamera, modelCenter.xz);

    // wind
    vec2 noiseUv1 = (modelPosition.xz * uWindNoiseScale1) + (uWindDirection * (uTime * 0.001 * uWindSpeed1));
    float noise1 = texture(uPerlinTexture, noiseUv1).r - 0.5;
    vec2 noiseUv2 = (modelPosition.xz * uWindNoiseScale2) + (uWindDirection * (uTime * 0.001 * uWindSpeed2));
    float noise2 = texture(uPerlinTexture, noiseUv2).g;
    float wind = noise1 * noise2;
    vec2 windForce = uWindDirection * wind * uWindStrength;

    // Apply wind
    modelPosition.x += windForce.x * tip;
    modelPosition.z += windForce.y * tip;

    // Lower grass
    
    // modelCenter (world pos) - uPlayerPosition (world pos)
    vec2 interactionUv = (modelCenter.xz - uPlayerPosition.xz) / uGrassSize + 0.5;
    
    // Flip Y to match the canvas coordinates (Canvas 0,0 is usually top-left)
    interactionUv.y = 1.0 - interactionUv.y;

    // texture glow strength
    float interactionStrength = texture(uInteractivePlaneTexture, interactionUv).r;
    interactionStrength = smoothstep(0.2, 0.3, interactionStrength);

    // Lower the grass based on interaction
    // modelPosition.y *= (1.0 - interactionStrength * tip);
    modelPosition.y *= 1.0 - interactionStrength;

    vec4 viewPosition = viewMatrix * modelPosition;
    vec4 projectionPositon = projectionMatrix * viewPosition;

    gl_Position = projectionPositon;

    // grass color
    vec3 grassColor = vec3(0.1, 0.4, 0.1);
    float gradient = tip * 0.8;
    vColor = vec3(grassColor * gradient);

}